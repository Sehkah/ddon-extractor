plugins {
    // built-in plugins
    id 'java'
    // Apply the application plugin to add support for building a CLI application in Java.
    id 'application'
    id 'jacoco'
    id 'project-report'
    // community plugins
    id 'org.beryx.jlink'
    id 'org.javamodularity.moduleplugin'
}

ext {
    orgApacheLoggingLog4jVersion = '2.20.0'
    comFasterxmlJacksonVersion = '2.15.0'
    junitVersion = '5.8.2'
    infoPicocliVersion = '4.7.3'
}

dependencies {
    // https://mvnrepository.com/artifact/org.apache.logging.log4j/log4j-api
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: "${orgApacheLoggingLog4jVersion}"
    // https://mvnrepository.com/artifact/org.apache.logging.log4j/log4j-core
    runtimeOnly group: 'org.apache.logging.log4j', name: 'log4j-core', version: "${orgApacheLoggingLog4jVersion}"
    // https://mvnrepository.com/artifact/com.fasterxml.jackson.dataformat/jackson-dataformat-yaml
    implementation group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: "${comFasterxmlJacksonVersion}"
    // https://mvnrepository.com/artifact/com.fasterxml.jackson.datatype/jackson-datatype-jsr310
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: "${comFasterxmlJacksonVersion}"
    // https://mvnrepository.com/artifact/info.picocli/picocli
    implementation group: 'info.picocli', name: 'picocli', version: "${infoPicocliVersion}"

    // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-api
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: "${junitVersion}"
    // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-params
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: "${junitVersion}"
    // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-engine
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: "${junitVersion}"
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.named('test') {
    // Use JUnit Platform for unit tests.
    useJUnitPlatform()
}

application {
    applicationDefaultJvmArgs = ['-Dfile.encoding=UTF-8']
    mainModule = 'org.sehkah.ddon.tools'
    mainClass = 'org.sehkah.ddon.tools.Main'
}

tasks.withType(Jar).configureEach {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes 'Main-Class': application.mainClass
    }
}

tasks.distTar.enabled = false

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages', '--ignore-signing-information']
    launcher {
        name = rootProject.name
        jvmArgs = ['-Dfile.encoding=UTF-8']
    }
    jpackage {
        jvmArgs = ['-Dfile.encoding=UTF-8']
        skipInstaller = true
        imageOptions += ['--win-console']
        installerOptions += [
                '--vendor', 'Sehkah',
                '--app-version', version
        ]
        if (org.gradle.internal.os.OperatingSystem.current().windows) {
            installerOptions += [
                    '--win-per-user-install', '--win-dir-chooser', '--win-menu'
            ]
        }
    }
    forceMerge('jackson', 'log4j')
    mergedModule {
        additive = true
        uses 'com.fasterxml.jackson.databind.Module'
        uses 'org.apache.logging.log4j.util.PropertySource'
        uses 'org.apache.logging.log4j.core.util.ContextDataProvider'
        uses 'org.apache.logging.log4j.core.util.WatchEventService'
        uses 'org.apache.logging.log4j.spi.Provider'
        uses 'org.apache.logging.log4j.message.ThreadDumpMessage.ThreadInfoFactory'
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(sourceCompatibility.toString())
        vendor = JvmVendorSpec.matching('Eclipse')
    }
}
